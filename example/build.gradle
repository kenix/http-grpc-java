plugins {
  id 'java'
  id 'com.google.protobuf' version "$protobufVersion"
}

ext {
  envoyProxyVersion = '0.4.1'
}

sourceSets {
  main {
    java {
      srcDir "${buildDir}/generated-sources/main/java"
      srcDir "${buildDir}/generated-sources/main/grpc"
      srcDir "${buildDir}/generated-sources/main/javapgv"
    }
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

dependencies {
  implementation "io.grpc:grpc-protobuf:$grpcVersion"
  implementation "io.grpc:grpc-stub:$grpcVersion"
  implementation "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:$envoyProxyVersion"
  implementation "io.envoyproxy.protoc-gen-validate:pgv-java-stub:$envoyProxyVersion"
  implementation 'org.springframework.boot:spring-boot-starter-web:2.4.2'
  implementation project(':lib')
  implementation 'net.devh:grpc-server-spring-boot-starter:2.10.1.RELEASE'
  implementation "io.envoyproxy.protoc-gen-validate:pgv-java-grpc:$envoyProxyVersion"

  annotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protocVersion"
  }

  generatedFilesBaseDir = "$buildDir/generated-sources"

  clean {
    delete generatedFilesBaseDir
  }

  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
    javapgv {
      artifact = "io.envoyproxy.protoc-gen-validate:protoc-gen-validate:$envoyProxyVersion"
    }
  }

  generateProtoTasks {
    all()*.plugins {
      grpc {}
      javapgv {
        option "lang=java"
      }
    }
  }
}
