apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
  // this should cover all version strategies
  isRelease = !(project.version.toString().toLowerCase().contains('snapshot'))
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = 'HTTP gRPC Spring Boot Starter'
        description = 'HTTP gRPC Spring Boot Starter'
        url = 'https://github.com/kenix/http-grpc-spring-boot-starter'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            id = 'kenix'
            name = 'Mike Zhao'
            email = 'zhongda.zhao@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/kenix/http-grpc-spring-boot-starter.git'
          developerConnection = 'scm:git:ssh@github.com:kenix/http-grpc-spring-boot-starter.git'
          url = 'https://github.com/kenix/http-grpc-spring-boot-starter'
        }
      }
    }
  }
  repositories {
    maven {
      credentials {
        username "${ossrhUsername}"
        password "${ossrhPassword}"
      }
      if (project.ext.isRelease) {
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      } else {
        url "https://oss.sonatype.org/content/repositories/snapshots"
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}