apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
  // this should cover all version strategies
  isRelease = !(project.version.toString().toLowerCase().contains('snapshot'))
}

// Include LICENSE
tasks.withType(Jar) {
  from(project.rootDir) {
    include 'LICENSE'
    into 'META-INF'
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

// Customize MANIFEST.MF
jar {
  manifest {
    attributes(
        'Implementation-Title': project.name,
        'Implementation-Version': project.version,
        'Implementation-Vendor': 'Mike Zhao'
    )
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'HTTP gRPC Spring Boot Starter'
        description = 'HTTP gRPC Spring Boot Starter'
        url = 'https://github.com/kenix/http-grpc-spring-web'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            id = 'kenix'
            name = 'Mike Zhao'
            email = 'zhongda.zhao@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/kenix/http-grpc-spring-web.git'
          developerConnection = 'scm:git:ssh@github.com:kenix/http-grpc-spring-web.git'
          url = 'https://github.com/kenix/http-grpc-spring-web'
        }
      }
    }
  }
  repositories {
    maven {
      credentials {
        username "${ossrhUsername}"
        password "${ossrhPassword}"
      }
      if (project.ext.isRelease) {
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      } else {
        url "https://oss.sonatype.org/content/repositories/snapshots"
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}